// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

version = '1.0.4'
group = 'me.melchor9000'
archivesBaseName = 'async-net'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile 'io.netty:netty-common:4.1.15.Final'
    compile 'io.netty:netty-buffer:4.1.15.Final'
    compile 'io.netty:netty-transport:4.1.15.Final'
    compile 'io.netty:netty-handler:4.1.15.Final'

    testCompile 'junit:junit:4.12'
}

// Change source sets to match the project
sourceSets {
    main {
        java {
            srcDirs = [ 'src' ]
        }
    }

    test {
        java {
            srcDirs = ['test']
        }
    }
}

// Set some information on the .jar
jar {
    manifest {
        attributes 'Implementation-Title': rootProject.name,
                   'Implementation-Version': version
    }
}

sourceCompatibility = JavaVersion.VERSION_1_7

// Create javadoc .jar
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

// Create source .jar
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// Tell gradle to set these artifacts too
artifacts {
    archives javadocJar, sourcesJar
}

// Signing artifacts
signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Async net'
                packaging 'jar'
                artifactId archivesBaseName

                description 'A basic asynchronous network library, based on netty'
                url 'https://github.com/melchor629/async-net'

                scm {
                    connection 'scm:git:git://github.com/melchor629/async-net.git'
                    developerConnection 'scm:git:ssh://github.com:melchor629/async-net.git'
                    url 'http://github.com/melchor629/async-net/tree/master'
                }

                licenses {
                    license {
                        name 'GPL-3.0'
                        url 'https://www.gnu.org/licenses/gpl.txt'
                    }
                }

                developers {
                    developer {
                        id 'melchor629'
                        name 'Melchor Garau Madrigal'
                        email 'melchor9000@gmail.com'
                    }
                }
            }
        }
    }
}
